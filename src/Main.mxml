<?xml version="1.0"?>
<s:Application xmlns:fx="http://ns.adobe.com/mxml/2009"
               xmlns:s="library://ns.adobe.com/flex/spark"
               width="100%" height="100%" frameRate="60"
               creationComplete="onCreationComplete(event)">

    <fx:Script>
        <![CDATA[
        import graph.Cell;
        import graph.Corner;
        import graph.Edge;

        import mx.core.UIComponent;
        import mx.events.FlexEvent;

        public static var map:Map;
        private var layer0:UIComponent;
        private var layer1:UIComponent;

        public function onCreationComplete(event:FlexEvent):void {
            layer0 = new UIComponent();
            addElement(layer0);
            layer1 = new UIComponent();
            addElement(layer1);

            createNewMap();

            addEventListener(MouseEvent.RIGHT_CLICK, function (event:MouseEvent):void {
                createNewMap();
            });

            addEventListener(MouseEvent.MOUSE_MOVE, function (event:MouseEvent):void {
                //draw();
                layer1.graphics.clear();

                var p:Point = new Point(event.stageX, event.stageY);
                var size:int = 50;
                var range:Rectangle = new Rectangle(p.x - size / 2, p.y - size / 2, size, size);
                var arr:Array = map.quadTree.query(range);
                for each (p in arr) {
                    layer1.graphics.beginFill(Color.red);
                    layer1.graphics.drawCircle(p.x, p.y, 1);
                    layer1.graphics.endFill();
                }

                layer1.graphics.lineStyle(1, Color.blue);
                layer1.graphics.drawRect(range.x, range.y, range.width, range.height)
            });
        }

        private function createNewMap():void {
            map = new Map(width, height, 200);

            draw();
        }

        private function draw():void {
            var g:Graphics = layer0.graphics;

            g.clear();
            g.lineStyle();
            g.beginFill(Color.black);
            g.drawRect(0, 0, width, height);
            g.endFill();

            for each (var cell:Cell in map.cells) {
                drawDot(g, cell.point, Color.white, 3);

                for each (var corner:Corner in cell.corners) {
                    drawDot(g, corner.point, corner.border ? Color.yellow : Color.blue, 3);
                }

                for each (var edge:Edge in cell.edges) {
                    if (edge.d0 && edge.d1)
                        drawLine(g, edge.d0.point, edge.d1.point, Color.red);
                    if (edge.v0 && edge.v1)
                        drawLine(g, edge.v0.point, edge.v1.point, Color.green);
                }
            }
        }

        private function drawDot(g:Graphics, point:Point, color:uint, size:int):void {
            g.beginFill(color);
            g.drawCircle(point.x, point.y, size);
            g.endFill();
        }

        private function drawLine(g:Graphics, point1:Point, point2:Point, color:uint):void {
            g.lineStyle(1, color);
            g.moveTo(point1.x, point1.y);
            g.lineTo(point2.x, point2.y);
            g.lineStyle();
        }
        ]]>
    </fx:Script>
</s:Application>