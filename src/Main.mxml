<?xml version="1.0"?>
<s:Application xmlns:fx="http://ns.adobe.com/mxml/2009"
               xmlns:s="library://ns.adobe.com/flex/spark"
               xmlns:ui="ui.*"
               width="100%"
               height="100%"
               frameRate="60"
               applicationComplete="onApplicationComplete(event)">

    <fx:Style source="/assets/css/global.css" />

    <fx:Script>
        <![CDATA[
        import global.Color;
        import global.Sort;
        import global.Util;

        import graph.Cell;
        import graph.Corner;
        import graph.Edge;

        import layers.geography.River;
        import layers.tectonics.TectonicPlate;
        import layers.wind.Gust;

        import mx.core.UIComponent;
        import mx.events.FlexEvent;
        import mx.events.ResizeEvent;

        import ui.Settings;

        import ui.UIEventType;

        public static var map:Map;

        private var layerDebug:UIComponent;
        private var layerOutlines:UIComponent;
        private var layerTectonicPlates:UIComponent;
        private var layerHeightMap:UIComponent;
        private var layerContinents:UIComponent;
        private var layerRelief:UIComponent;
        private var layerOcean:UIComponent;
        private var layerWind:UIComponent;
        private var layerPrecipitation:UIComponent;
        private var layerTemperature:UIComponent;
        private var layerFlow:UIComponent;
        private var layerRivers:UIComponent;

        private var layers:Array;
        private var g:Graphics;

        public function onApplicationComplete(event:FlexEvent):void
        {
            // Set up layers
            layerDebug = new UIComponent();
            layerDebug.name = "debug";
            layerOutlines = new UIComponent();
            layerOutlines.name = "outlines";
            layerTectonicPlates = new UIComponent();
            layerTectonicPlates.name = "tectonics";
            layerHeightMap = new UIComponent();
            layerHeightMap.name = "elevation";
            layerContinents = new UIComponent();
            layerContinents.name = "continents";
            layerRelief = new UIComponent();
            layerRelief.name = "relief";
            layerOcean = new UIComponent();
            layerOcean.name = "ocean";
            layerWind = new UIComponent();
            layerWind.name = "wind";
            layerPrecipitation = new UIComponent();
            layerPrecipitation.name = "precipitation";
            layerTemperature = new UIComponent();
            layerTemperature.name = "temperature";
            layerFlow = new UIComponent();
            layerFlow.name = "flow";
            layerRivers = new UIComponent();
            layerRivers.name = "rivers";

            //layers = [layerTectonicPlates, layerHeightMap, layerContinents, layerRelief, layerOcean, layerWind, layerWater, layerPrecipitation, layerFlow, layerRivers, layerTemperature, layerOutlines, layerDebug];
            layers = [layerTectonicPlates, layerHeightMap, layerContinents, layerRelief, layerOcean, layerWind, layerPrecipitation, layerFlow, layerRivers, layerOutlines, layerDebug];
            for each (var layer:UIComponent in layers)
                layerGroup.addElement(layer);

            // Initial layers
            var initialLayers:Array = [];
            for each (var layerName:String in Settings.initialLayers)
            {
                for each (var l:UIComponent in layers)
                    if (layerName == l.name)
                        initialLayers.push(l);
            }

            for each (layer in layers)
                layer.visible = false;

            for each (layer in initialLayers)
                layer.visible = true;

            // Make Map
            makeMap();
            draw();

            // Interactivity
            userInterface.addLayers(layers.reverse());

            addEventListener(MouseEvent.CLICK, onClick);

            userInterface.addEventListener(UIEventType.MAKE_MAP, function (event:Event):void
            {
                userInterface.setToWorking();
                setTimeout(function ():void
                {
                    makeMap();
                    draw();
                }, 100);
            });

            stage.nativeWindow.addEventListener(Event.CLOSING, onExit);
        }

        private function onExit(event:Event):void
        {
            event.preventDefault();

            var initialLayers:Array = [];
            for each (var layer:UIComponent in layers)
                if (layer.visible)
                    initialLayers.push(layer.name);

            Settings.initialLayers = initialLayers;

            stage.nativeWindow.close();
        }

        private function draw():void
        {
            drawDebug();
            drawOutlines();
            drawTectonicPlates();
            drawColorHeightMap();
            drawContinents();
            drawRelief();
            drawOcean();

            drawWind();
            drawPrecipitation();
            //drawTemperature();
            //drawWater();
            drawFlow();
            drawRivers();
        }

        private function toggleLayer(layer:UIComponent):void
        {
            layer.visible = !layer.visible;
            var onOrOff:String = layer.visible ? "ON" : "OFF";
            Util.log(Util.capitalizeFirstLetter(layer.name) + " [" + onOrOff + "]");
            userInterface.updateLayer(layer);
        }

        private function onClick(event:MouseEvent):void
        {
            var p:Point = new Point(event.localX, event.localY);
            var arr:Array = Util.toArray(map.quadTree.queryFromPoint(p, 10));
            if (arr.length > 0)
            {
                var t:Point = Util.closestPoint(p, arr);
                var c:Cell = map.getCellByPoint(t);
                describe(c);

                //trace("> " + c.lowestNeighborBelow.elevation);
            }
        }

        private function describe(c:Cell):void
        {
            trace("Cell #" + c.index);
            for each (var r:River in c.rivers)
                trace("River index=" + r.index + " (length=" + r.cells.length + ")");
//            trace("  elevation=" + Util.round(c.elevation));
//            trace("  ocean=" + c.ocean);
        }

        private function makeMap():void
        {
            map = new Map(1200, 600, Util.stringToSeed(userInterface.seedPhrase));

            userInterface.setToReady();
        }

        private function drawContinents():void
        {
            drawOnLayer(layerContinents);

            for each (var cell:Cell in map.cells)
                if (!cell.ocean)
                    fillCell(cell, Color.green);
        }

        private function drawOcean():void
        {
            drawOnLayer(layerOcean);

            for each (var cell:Cell in map.cells)
                if (cell.ocean)
                    fillCell(cell, Color.blue, .6);
        }

        private function drawWind():void
        {
            drawOnLayer(layerWind);

            for each (var gust:Gust in map.wind.gusts)
                drawPointer(gust.point, gust.angle, Color.red, 5, gust.strength);
        }

        private function drawPrecipitation():void
        {
            drawOnLayer(layerPrecipitation);

            for each (var cell:Cell in map.cells)
                fillCell(cell, Color.darkBlue, cell.moisture);
        }

        private function drawTemperature():void
        {
            drawOnLayer(layerTemperature);

            for each (var cell:Cell in map.cells)
                fillCell(cell, Util.colorBetweenColors(Color.blue, Color.orange, cell.temperature), .7);
        }

        private function drawFlow():void
        {
            drawOnLayer(layerFlow);

            for each (var cell:Cell in map.cells)
            {
                if (cell.lowestNeighborBelow && !cell.ocean)
                {
                    g.lineStyle(1, Color.darkBlue);
                    g.moveTo(cell.point.x, cell.point.y);
                    g.lineTo(cell.lowestNeighborBelow.point.x, cell.lowestNeighborBelow.point.y);
                    drawPointer(cell.point, Util.angleBetweenTwoPoints(cell.point, cell.lowestNeighborBelow.point), Color.darkBlue);
                }
            }
        }

        private function drawRivers():void
        {
            drawOnLayer(layerRivers);

            map.hydrology.rivers.sort(Sort.riverByLength);
            for each (var river:River in map.hydrology.rivers)
            {
                var color:uint = Math.random() * Color.white;
                //color = river.type == River.STEM ? Color.red : Color.yellow;

                for each (var cell:Cell in river.cells)
                {
                    if (cell != river.end || cell.lowestNeighborBelow)
                    {
//                        if (cell == river.start)
//                        {
//                            g.lineStyle();
//                            drawDot(cell.point, color, 5);
//                        }

                        g.lineStyle(1, color);
                        g.moveTo(cell.point.x, cell.point.y);
                        g.lineTo(cell.lowestNeighborBelow.point.x, cell.lowestNeighborBelow.point.y);
                        drawPointer(cell.point, Util.angleBetweenTwoPoints(cell.point, cell.lowestNeighborBelow.point), color);
                    }
                }
            }
        }

        private function drawRelief():void
        {
            drawOnLayer(layerRelief);

            for each (var cell:Cell in map.cells)
            {
                // Create triangles
                for each (var edge:Edge in cell.edges)
                {
                    if (!edge.v0 || !edge.v1)
                        continue;

                    g.beginFill(Color.black, calculateLighting(cell, edge.v0, edge.v1) * .8);
                    g.moveTo(cell.point.x, cell.point.y);
                    g.lineTo(edge.v0.point.x, edge.v0.point.y);
                    g.lineTo(edge.v1.point.x, edge.v1.point.y);
                    g.endFill();
                }
            }
        }

        public function calculateLighting(p:Cell, r:Corner, s:Corner):Number
        {
            if (!p || !r || !s)
                return 0;

            var lightVector:Vector3D = new Vector3D(1, 1, 0);

            var a:Vector3D = new Vector3D(p.point.x, p.point.y, p.elevation);
            var b:Vector3D = new Vector3D(r.point.x, r.point.y, r.elevation);
            var c:Vector3D = new Vector3D(s.point.x, s.point.y, s.elevation);

            var normal:Vector3D = b.subtract(a).crossProduct(c.subtract(a));
            if (normal.z < 0)
                normal.scaleBy(-1);
            normal.normalize();

            var light:Number = 0.5 + 35 * normal.dotProduct(lightVector);
            if (light < 0) light = 0;
            else if (light > 1) light = 1;

            return light;
        }

        private function drawColorHeightMap():void
        {
            drawOnLayer(layerHeightMap);

            for each (var cell:Cell in map.cells)
                fillCell(cell, colorFromHeightMap(cell.elevation));
        }

        private function colorFromHeightMap(height:Number):uint
        {
            var colors:Array = [Color.darkBlue, Color.blue, Color.green, Color.yellow, Color.orange, Color.red];

            var preciseIndex:Number = (colors.length - 1) * height;
            var index:int = Math.floor(preciseIndex);

            var color:uint = colors[index];
            if (index < colors.length - 1)
                color = Util.colorBetweenColors(colors[index], colors[index + 1], preciseIndex - index);

            return color;
        }

        private function drawDebug():void
        {
            drawOnLayer(layerDebug);
        }

        private function drawOutlines():void
        {
            drawOnLayer(layerOutlines);

            for each (var cell:Cell in map.cells)
                for each (var edge:Edge in cell.edges)
                    if (edge.v0 && edge.v1)
                        drawLine(edge.v0.point, edge.v1.point, Color.black);
        }

        private function drawTectonicPlates():void
        {
            drawOnLayer(layerTectonicPlates);

            for each (var cell:Cell in map.cells)
            {
                if (cell.tectonicPlate && cell.tectonicPlate.type != TectonicPlate.DEEP)
                    fillCell(cell, cell.tectonicPlate.color);
                //fillCell(g, cell, cell.tectonicPlate.type == TectonicPlate.CONTINENTAL ? 0xE74856 : 0x8E8CD8);

                // Draw tectonic plate direction
                if (cell.tectonicPlateBorder)
                    drawPointer(cell.point, cell.tectonicPlateDirection, Color.black);
            }
        }

        private function fillCell(cell:Cell, color:uint, alpha:Number = 1):void
        {
            // Draw a filled cell
            g.beginFill(color, alpha);
            for each (var edge:Edge in cell.edges)
            {
                if (edge.v0 && edge.v1)
                {
                    g.moveTo(edge.v0.point.x, edge.v0.point.y);
                    g.lineTo(cell.point.x, cell.point.y);
                    g.lineTo(edge.v1.point.x, edge.v1.point.y);
                }
            }
            g.endFill();
        }

        private function drawDot(point:Point, color:uint, size:int):void
        {
            g.beginFill(color);
            g.drawCircle(point.x, point.y, size);
            g.endFill();
        }

        private function drawLine(point1:Point, point2:Point, color:uint, weight:Number = 1):void
        {
            g.lineStyle(weight, color);
            g.moveTo(point1.x, point1.y);
            g.lineTo(point2.x, point2.y);
            g.lineStyle();
        }

        private function drawPointer(point:Point, direction:Number, color:uint, distance:Number = 7, pointerAlpha:Number = 1):void
        {
            // Draw pointer
            g.lineStyle();
            g.beginFill(color, pointerAlpha);
            //g.drawCircle(point.x, point.y, 2);
            //g.endFill();
            var p0:Point = Util.pointFromAngleAndDistance(point, direction - 90, distance / 2);
            var p1:Point = Util.pointFromAngleAndDistance(point, direction, distance);
            var p2:Point = Util.pointFromAngleAndDistance(point, direction + 90, distance / 2);

            g.moveTo(p0.x, p0.y);
            //var head:Point = Util.pointFromAngleAndDistance(point, direction, distance);
            g.lineTo(p1.x, p1.y);
            g.lineTo(p2.x, p2.y);
            g.lineTo(p0.x, p0.y);
            g.lineStyle();
        }

        private function onLayerGroupContainerResize(event:ResizeEvent):void
        {
//            trace(event.target.width, event.target.height);
//            trace(event.target.contentWidth, event.target.contentHeight);

            var t:Object = event.target;
            t.height = (t.width / t.contentWidth) * t.contentHeight;
        }

        private function drawOnLayer(layer:UIComponent):void
        {
            g = layer.graphics;
            g.clear();
        }
        ]]>
    </fx:Script>

    <s:VGroup width="100%"
              height="100%"
              gap="0">

        <s:Spacer height="{userInterface.toolBar.height}" />

        <s:Group width="100%"
                 height="100%">

            <s:VGroup id="layerGroupContainer"
                      padding="15"
                      width="100%"
                      height="100%"
                      resizeMode="scale"
                      resize="onLayerGroupContainerResize(event)">
                <s:Group id="layerGroup"
                         width="1200"
                         height="600" />
            </s:VGroup>

        </s:Group>

    </s:VGroup>

    <ui:UserInterface id="userInterface"
                      width="100%"
                      height="100%" />

</s:Application>